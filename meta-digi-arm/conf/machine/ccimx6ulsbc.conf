#@TYPE: Machine
#@NAME: SBC for ConnectCore for i.MX6UL
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 6UL SBC.

# Include the machine configuration for Digi's ConnectCore 6UL.
include conf/machine/include/ccimx6ul.inc

# U-Boot configurations
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG ??= "ccimx6ulsbc1GB ccimx6ulsbc512MB ccimx6ulsbc"
UBOOT_CONFIG[ccimx6ulsbc1GB] = "ccimx6ulsbc1GB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6ulsbc512MB] = "ccimx6ulsbc512MB_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"
UBOOT_CONFIG[ccimx6ulsbc] = "ccimx6ulsbc_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"

KERNEL_DEVICETREE ?= " \
    imx6ul-ccimx6ulsbc.dtb \
    imx6ul-ccimx6ulsbc-wb.dtb \
    imx6ul-ccimx6ulsbc-id135.dtb \
    imx6ul-ccimx6ulsbc-id136.dtb \
"
KERNEL_DEVICETREE:use-mainline-bsp ?= "imx6ul-ccimx6ulsbcpro.dtb"

KERNEL_DEFCONFIG ?= "arch/arm/configs/ccimx6ul_defconfig"

SERIAL_CONSOLES ?= "115200;ttymxc4"

# Bluetooth tty
BT_TTY ?= "ttymxc0"

# XBee
XBEE_TTY ?= "ttymxc1"

# Image FS types
IMAGE_FSTYPES ?= '${@bb.utils.contains("IMAGE_FEATURES", "read-only-rootfs", \
				       "tar.bz2 boot.ubifs recovery.ubifs squashfs", \
				       "tar.bz2 ubifs boot.ubifs recovery.ubifs", d)}'

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    u-boot-ccimx6ulsbc1GB.imx \
    u-boot-ccimx6ulsbc512MB.imx \
    u-boot-ccimx6ulsbc.imx \
"
