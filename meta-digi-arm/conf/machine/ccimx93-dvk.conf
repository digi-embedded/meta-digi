#@TYPE: Machine
#@NAME: ConnectCore 93 Development Kit
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 93 DVK

DIGI_SOM = "ccimx93"
MACHINEOVERRIDES =. "mx93:${DIGI_FAMILY}:${DIGI_SOM}:"

# Include the machine configuration for Digi's ConnectCore 9 module
include conf/machine/include/ccimx9.inc

# U-Boot configurations
UBOOT_CONFIG ??= "ccimx93-dvk"
UBOOT_CONFIG[ccimx93-dvk] = "ccimx93-dvk_defconfig"

# imx-boot configurations
ATF_PLATFORM = "imx93"
IMX_BOOT_SOC_TARGET = "iMX93"
IMXBOOT_TARGETS = "flash_singleboot flash_singleboot_a0"
# Generate images for all SOC revisions (keep the override as otherwise the
# one in "use-imx-security-controller-firmware.bbclass" takes precedence)
SECO_FIRMWARE_NAME:ccimx93 = "mx93??-ahab-container.img"

KERNEL_DEVICETREE ?= " \
    digi/ccimx93-dvk.dtb \
    digi/_ov_board_dlc0200ccp04df-mipi-dsi_ccimx93-dvk.dtbo \
    digi/_ov_board_lvds_ccimx93-dvk.dtbo \
    digi/_ov_board_enet2_ccimx9-dvk.dtbo \
    digi/_ov_board_mikroe-accel2-click_ccimx9-dvk.dtbo \
    digi/_ov_board_mikroe-gyro-click_ccimx9-dvk.dtbo \
    digi/_ov_som_bt_ccimx9.dtbo \
    digi/_ov_som_bt-dtm_ccimx9.dtbo \
    digi/_ov_som_npu_ccimx93.dtbo \
    digi/_ov_som_wifi_ccimx9.dtbo \
    ${@bb.utils.contains('DISTRO_FEATURES', 'tsn', 'digi/_ov_board_eqos-tsn_ccimx9-dvk.dtbo', '', d)} \
"

# Add additional firmware
MACHINE_FIRMWARE:append = " ethos-u-firmware"

IMAGE_BOOT_FILES:append:use-nxp-bsp = " \
    imx93_m33_TCM_rpmsg_lite_pingpong_rtos_linux_remote.bin \
    imx93_m33_TCM_rpmsg_lite_str_echo_rtos.bin \
"
WKS_FILE_DEPENDS:append = " imx-m33-demos"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    imx-boot-ccimx93-dvk.bin \
    imx-boot-ccimx93-dvk-A0.bin \
"
