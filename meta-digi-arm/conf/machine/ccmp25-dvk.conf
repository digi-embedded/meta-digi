#@TYPE: Machine
#@NAME: ccmp25-dvk
#@DESCRIPTION: Configuration for Digi ConnectCore MP25 DVK
#@NEEDED_BSPLAYERS: layers/meta-digi

# Include the machine configuration for Digi's ConnectCore MP2.
include conf/machine/include/ccmp2.inc

# Define specific familly common machine name
DIGI_SOM = "ccmp25"
MACHINEOVERRIDES =. "stm32mp2common:stm32mp25common:${DIGI_FAMILY}:${DIGI_SOM}:"

# =========================================================================
# SOC
# =========================================================================
STM32MP_SOC_NAME = "stm32mp25"

# =========================================================================
# Chip architecture
# =========================================================================
DEFAULTTUNE = "cortexa35"
include conf/machine/include/arm/armv8a/tune-cortexa35.inc

# =========================================================================
# boot device
# =========================================================================
# Configure the list of boards that enable EMMC/SDCARD
DEVICE_BOARD_ENABLE:EMMC   += "${STM32MP_DEVICETREE}"
DEVICE_BOARD_ENABLE:SDCARD += "${@bb.utils.contains('BOOTDEVICE_LABELS', 'sdcard', '${STM32MP_DEVICETREE}', '', d)}"

# =========================================================================
# U-Boot configs
# =========================================================================
# Last one is the default (the one the symlinks point at)
UBOOT_CONFIG = "ccmp25-dvk"
UBOOT_CONFIG[ccmp25-dvk] = "ccmp25-dvk_defconfig,,u-boot-dtb.${UBOOT_SUFFIX}"

# =========================================================================
# Machine settings
# =========================================================================
# Define list of devicetree per supported storage
STM32MP_DEVICETREE ??= "ccmp25-dvk"
# Extra DTB for board - need to specify it with .dtb ...
STM32MP_KERNEL_DEVICETREE:ccmp25-dvk += " \
    ccmp25-dvk.dtb \
    _ov_board_e55rb-i-mw346-c-mipi-dsi_ccmp25-dvk.dtbo \
    _ov_board_g101evn010-lvds_ccmp25-dvk.dtbo \
    _ov_board_mikroe-accel2-click_ccmp25-dvk.dtbo \
    _ov_board_mikroe-gyro-click_ccmp25-dvk.dtbo \
    _ov_board_mikroe-i2c-to-spi-click_ccmp25-dvk.dtbo \
    _ov_board_mikroe-mcp2518fd-click_ccmp25-dvk.dtbo \
    _ov_board_n25q256a-spi-nor-flash_ccmp25-dvk.dtbo \
    _ov_board_nhd-3-5-640480ef-msxp-mipi-dsi_ccmp25-dvk.dtbo \
    _ov_board_usb-3-0-typec_ccmp25-dvk.dtbo \
    _ov_som_bt_ccmp25.dtbo \
    _ov_som_wifi_ccmp25.dtbo \
"

# =========================================================================
# optee
# =========================================================================
# Map OPTEE configuration to device tree list
OPTEE_CONF = "${STM32MP_DEVICETREE}"

# =========================================================================
# Machine features
# =========================================================================
MACHINE_FEATURES += "splashscreen"
MACHINE_FEATURES += "watchdog"
MACHINE_FEATURES += "bluetooth"
MACHINE_FEATURES += "wifi"
MACHINE_FEATURES += "usbg0"

# GPU
MACHINE_FEATURES += "${@'gpu' if d.getVar('ACCEPT_EULA_'+d.getVar('MACHINE')) == '1' else ''}"
MACHINE_FEATURES += "openvx"
MACHINE_FEATURES += "opencl"
MACHINE_FEATURES += "vulkan"

# M33
MACHINE_FEATURES += "m33copro"

# Disable external dt
EXTERNAL_DT_ENABLED = "0"

# Flashlayout
# No metadata generated and supported for the moment
STM32MP_METADATA_DATA ?= ""

# GPU have vulkan
DISTRO_FEATURES:append = " ${@bb.utils.contains('DISTRO', 'openstlinux-eglfs', '', 'vulkan', d)} "
DISTRO_FEATURES:append = " opencl "

# =========================================================================
# Firmware
# =========================================================================
MACHINE_FIRMWARE:append = " firmware-murata-infineon firmware-murata-infineon-mfgtest"

MACHINE_EXTRA_RRECOMMENDS += " \
    ${MACHINE_FIRMWARE} \
"

# =========================================================================
# Image
# =========================================================================
# Image FS types
IMAGE_FSTYPES += '${@bb.utils.contains("IMAGE_FEATURES", "read-only-rootfs", \
                                       "boot.vfat recovery.vfat squashfs tar.bz2", \
                                       "boot.vfat recovery.vfat ext4.gz tar.bz2", d)}'

# Default image for install scripts
DEFAULT_IMAGE_NAME ?= "dey-image-qt"

# Remove additional bluetooth packages
MACHINE_BLUETOOTH_EXTRA_INSTALL = ""

# For populate_sdk, gcc-arm-none-eabi_11 has a libpython3 dependency, so we remove it.
ST_TOOLS_FOR_SDK:remove = "nativesdk-gcc-arm-none-eabi"

# XBee
XBEE_RESET_N_GPIO ?= "GPIOD@6"
XBEE_SLEEP_RQ_GPIO ?= ""
XBEE_TTY ?= "ttySTM2"

# =========================================================================
# alsa
# =========================================================================
ALSA_LIST = ""

# Disable use of vendorfs and userfs partitions
ST_VENDORFS = "0"
ST_USERFS   = "0"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    arm-trusted-firmware/tf-a-ccmp25-dvk-emmc.stm32 \
    fip/fip-ccmp25-dvk-optee.bin \
    metadata-ccmp25-dvk.bin \
"

# Per-machine DISTRO_FEATURES customization
MACHINE_DISTRO_FEATURES_REMOVE = "x11"

# Default ROOTFS max size for image being built to this value
IMAGE_ROOTFS_MAXSIZE = "1572864"

# Default overlayfs_etc mount point and type
OVERLAYFS_ETC_MOUNT_POINT ?= "/mnt/data"
OVERLAYFS_ETC_DEVICE ?= "/dev/mmcblk0p10"
OVERLAYFS_ETC_FSTYPE ?= "ext4"