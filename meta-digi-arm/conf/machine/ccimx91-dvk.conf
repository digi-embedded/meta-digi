#@TYPE: Machine
#@NAME: ConnectCore 91 Development Kit
#@DESCRIPTION: Machine configuration for Digi's ConnectCore 91 DVK

DIGI_SOM = "ccimx91"
MACHINEOVERRIDES =. "mx91:${DIGI_FAMILY}:${DIGI_SOM}:"

# Include the machine configuration for Digi's ConnectCore 9 module
include conf/machine/include/ccimx9.inc

IMXBOOT_TARGETS = "flash_singleboot"

# U-Boot configurations
UBOOT_CONFIG ??= "ccimx91-dvk"
UBOOT_CONFIG[ccimx91-dvk] = "ccimx91-dvk_defconfig"

# imx-boot configurations
ATF_PLATFORM = "imx91"
IMX_BOOT_SOC_TARGET = "iMX91"
IMXBOOT_TARGETS = "flash_singleboot"
SECO_FIRMWARE_NAME = "mx91a0-ahab-container.img"

BOOTLOADER_SEEK_USERDATA = "32"

KERNEL_DEVICETREE ?= " \
    digi/ccimx91-dvk.dtb \
    digi/_ov_board_enet2_ccimx9-dvk.dtbo \
    digi/_ov_board_mikroe-accel2-click_ccimx9-dvk.dtbo \
    digi/_ov_board_mikroe-gyro-click_ccimx9-dvk.dtbo \
    digi/_ov_som_bt_ccimx9.dtbo \
    digi/_ov_som_bt-dtm_ccimx9.dtbo \
    digi/_ov_som_wifi_ccimx9.dtbo \
    ${@bb.utils.contains('DISTRO_FEATURES', 'tsn', 'digi/_ov_board_eqos-tsn_ccimx9-dvk.dtbo', '', d)} \
"

# Default image for install scripts
DEFAULT_IMAGE_NAME ?= "core-image-base"

# Set this platform without graphical support
IS_HEADLESS = "true"

# Boot artifacts to be copied from the deploy dir to the installer ZIP
BOOTABLE_ARTIFACTS = " \
    imx-boot-ccimx91-dvk.bin \
"

# Add secure enclave
SOC_TOOLS_TEST:append:mx91-nxp-bsp = " imx-secure-enclave"
# Add AFE support
NXPAFE_INSTALL:mx91-nxp-bsp = "nxp-afe nxp-afe-voiceseeker"
